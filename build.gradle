apply plugin: 'groovy'
apply plugin: 'maven'

version = '0.8'

group = 'com.github.dfrommi'

sourceCompatibility = 1.6
targetCompatibility = 1.6

ext {
	groovy = [version: '2.3.10']
}

repositories {
	mavenCentral()
}

sourceSets {
	integrationTest {
		groovy {
			compileClasspath += main.output + test.output
			runtimeClasspath += main.output + test.output
			srcDir file('src/integration-test/groovy')
		}
		resources.srcDir file('src/integration-test/resources')
	}
}

configurations {
	groovyDoc
	integrationTestCompile.extendsFrom testCompile
	integrationTestRuntime.extendsFrom testRuntime
}

dependencies {
	compile "org.codehaus.groovy:groovy-all:$groovy.version"
	testCompile "org.spockframework:spock-core:1.0-groovy-2.4"
	integrationTestCompile "org.spockframework:spock-core:1.0-groovy-2.4"
	groovyDoc "org.codehaus.groovy:groovy-groovydoc:$groovy.version"
	groovyDoc "org.codehaus.groovy:groovy-ant:$groovy.version"
}

groovydoc {
	groovyClasspath = configurations.groovyDoc
	destinationDir = file('gh-pages')
}

tasks.withType(Test) {
	reports.html.destination = file("${reporting.baseDir}/${name}")
}

task integrationTest(type: Test) {
	systemProperties << [ pandoc: project.properties.pandoc ]
	testClassesDir = sourceSets.integrationTest.output.classesDir
	classpath = sourceSets.integrationTest.runtimeClasspath
	include '**/*Spec.*'
	outputs.upToDateWhen { false }
}

task wrapper(type: Wrapper) {
	gradleVersion = '3.2.1'
}
