apply plugin: 'groovy'
apply plugin: 'maven'

version = '0.2.1'

group = 'com.github.dfrommi'

ext {
	groovy = [version: '2.3.10']
}

repositories {
	mavenCentral()
}

sourceSets {
	integrationTest {
		groovy {
			compileClasspath += main.output + test.output
			runtimeClasspath += main.output + test.output
			srcDir file('src/integration-test/groovy')
		}
		resources.srcDir file('src/integration-test/resources')
	}
}

configurations {
	groovyDoc
	integrationTestCompile.extendsFrom testCompile
	integrationTestRuntime.extendsFrom testRuntime
}

dependencies {
	compile "org.codehaus.groovy:groovy-all:$groovy.version"
	testCompile "junit:junit:4.11"
	integrationTestCompile "junit:junit:4.11"
	groovyDoc "org.codehaus.groovy:groovy-groovydoc:$groovy.version"
	groovyDoc "org.codehaus.groovy:groovy-ant:$groovy.version"
}

groovydoc {
	groovyClasspath = configurations.groovyDoc
	destinationDir = file('gh-pages')
}

tasks.withType(Test) {
	reports.html.destination = file("${reporting.baseDir}/${name}")
}

task integrationTest(type: Test) {
	testClassesDir = sourceSets.integrationTest.output.classesDir
	classpath = sourceSets.integrationTest.runtimeClasspath
	include '**/*Test.*'
	outputs.upToDateWhen { false }
}

task wrapper(type: Wrapper) {
	gradleVersion = '2.8'
}